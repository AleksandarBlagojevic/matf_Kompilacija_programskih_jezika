%{
    #include <iostream>
    #include <cstdlib>
    #include <string>
    #include <map>
    #include "logika.hpp"
    
    #define YYDEBUG 1
    
    using namespace std;
    
    void yyerror(string poruka){
        cerr << poruka << endl;
        exit(EXIT_FAILURE);
    }
    
    extern int yylex();
    
    map<string, Logika*> tablica;
    
%}

%union{
    int vrednost;
    string *ime;
    Logika *l;
}

%token presek_token unija_token akko_token sledi_token dodela_token

%token <vrednost> true_token false_token

%token <ime> id_token

%type <l> E


%left unija_token
%left presek_token
%left sledi_token akko_token
%left '!'


%%

Program: NaredbaN '\n' Program
    | '\n'
    |
    ;
    
NaredbaN: E                      {
                                    if($1->vrednost()==1)
                                        cout << "True" << endl;
                                    else if($1->vrednost()==0)
                                        cout << "False" << endl;
                                    else
                                        cout << "Undef" << endl;
                                    delete $1;
                                }
    | id_token dodela_token E   {
                                    tablica[*$1] = $3;
                                    delete $1;
                                }
    ;
    
E: E presek_token E     {   $$ = new I($1,$3);}
 | E unija_token E      {   $$ = new Ili($1,$3); }
 | E sledi_token E      {   $$ = new Sledi($1,$3);  }
 | E akko_token E       {   $$ = new Akko($1,$3);  }
 | '!' E                {   $$ = new Negacija($2); }
 | '(' E ')'            {   $$ = $2;    }
 | id_token             {   
                            map<string,Logika*>::iterator i = tablica.find(*$1);
                            if(i == tablica.end())
                                tablica[*$1] = new Konstanta(-1);
                            $$ = new Promenljiva(*$1);
                            delete $1;
                        }
 | true_token           {   $$ = new Konstanta($1);    }
 | false_token          {   $$ = new Konstanta($1);    }
 ;
   
    
%%

int main(){
    //yydebug = 0;
    yyparse();
    return EXIT_SUCCESS;
}
